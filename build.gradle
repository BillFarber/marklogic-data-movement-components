plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "com.github.jk1.dependency-license-report" version "1.17"

    id "net.saliman.properties" version "1.5.1"
    id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

group = "com.marklogic"
version = "2.4.0"

sourceCompatibility = "8"
targetCompatibility = "8"

ext {
    taskGroup = project.name
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'com.marklogic:marklogic-client-api:5.5.0'
    implementation 'org.slf4j:slf4j-api:1.7.31'

    testImplementation "com.marklogic:marklogic-junit5:1.1.0"

    // Forcing Spring to use logback instead of commons-logging
    testImplementation "ch.qos.logback:logback-classic:1.2.4"
    testImplementation "org.slf4j:jcl-over-slf4j:1.7.31"
    testImplementation "org.slf4j:slf4j-api:1.7.31"
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc
}
javadoc.failOnError = false

artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign configurations.archives
}

task generatePomForDependencyGraph(dependsOn: "generatePomFileForMainJavaPublication") {
    description = "Prepare for a release by making a copy of the generated pom file in the root directory so that it " +
            "can enable Github's Dependency Graph feature, which does not yet support Gradle"
    doLast {
        def preamble = '<?xml version="1.0" encoding="UTF-8"?>'
        def comment = "<!--\n" +
                "This file was generated via Gradle and is being used primarily for github's Dependency Graph feature.\n" +
                "It is not intended to be used to build this project.\n" +
                "-->"
        def fileText = file("build/publications/mainJava/pom-default.xml").getText()
        file("pom.xml").setText(fileText.replace(preamble, preamble + comment))
    }
}

publishing {
    publications {
        mainJava(MavenPublication) {
            pom {
                name = "${group}:${project.name}"
                description = "Reusable components that depend solely on MarkLogic's DMSDK"
                packaging = "jar"
                url = "https://github.com/marklogic-community/${project.name}"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = "marklogic"
                        name = "MarkLogic Github Contributors"
                        email = "general@developer.marklogic.com"
                        organization = "MarkLogic"
                        organizationUrl = "https://www.marklogic.com"
                    }
                }
                scm {
                    url = "git@github.com:marklogic-community/${project.name}.git"
                    connection = "scm:git@github.com:marklogic-community/${project.name}.git"
                    developerConnection = "scm:git@github.com:marklogic-community/${project.name}.git"
                }
            }
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = "central"
            url = mavenCentralUrl
            credentials {
                username mavenCentralUsername
                password mavenCentralPassword
            }
        }
    }
}


task copyTableauSourceFiles(type: Copy, group: taskGroup) {
    description = "Copy Tableau-dependent source files into the main source directory so they can be compiled and packaged into a jar for publishing. " +
            "You'll first need to add the Tableau jars to the 'dependencies' block as described in src/tableau/README.md ."
    from "src/tableau/main/java"
    into "src/main/java"
}

task createTableauPublication(group: taskGroup) {
    description = "Publish a version of ${project.name} that includes the Tableau-dependent source files under src/tableau/main/java"
    dependsOn = ["copyTableauSourceFiles", "publishToMavenLocal"]
    doLast {
        println "Version ${version} of ${project.name} has been published to your local Maven repository, which defaults to ~/.m2/repository."
        println "You can depend on this artifact in another Gradle project by adding mavenLocal() to your list of repositories."
    }
}
publishToMavenLocal.mustRunAfter copyTableauSourceFiles

// See https://github.com/snyk/gradle-plugin for more information
snyk {
    //arguments = '--all-sub-projects'
    severity = 'low'
    api = snykToken
    autoDownload = true
    autoUpdate = true
}
