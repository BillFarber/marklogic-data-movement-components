plugins {
    id "java-library"
    id "maven-publish"
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.jk1.dependency-license-report" version "1.3"

    id "net.saliman.properties" version "1.5.1"
    id "io.snyk.gradle.plugin.snykplugin" version "0.4"
}

sourceCompatibility = "8"
targetCompatibility = "8"

ext {
    taskGroup = project.name
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://developer.marklogic.com/maven2/" }
    mavenLocal()
}

dependencies {
    api "com.marklogic:marklogic-client-api:5.3.0"

    testImplementation "junit:junit:4+"
    // Sticking with the latest 4.3.x for tests; including the latest 5.2.x release oddly resulted in the servlet package
    // being required
    testImplementation "org.springframework:spring-test:4.3.29.RELEASE"
    testImplementation "com.marklogic:ml-javaclient-util:4.1.0-SNAPSHOT"

    // Forcing Spring to use logback instead of commons-logging
    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation group: "org.slf4j", name: "jcl-over-slf4j", version: "1.7.30"
    testImplementation group: "org.slf4j", name: "slf4j-api", version: "1.7.30"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from file(javadocsDir + "/" + project.name)
}

javadoc.destinationDir = file(javadocsDir + "/" + project.name)
javadoc.failOnError = false

publishing {
    publications {
        mainJava(MavenPublication) {
            from components.java
        }
        sourcesJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
        mavenJavadoc(MavenPublication) {
            artifact javadocJar
        }
    }
}

if (project.hasProperty("myBintrayUser")) {
    bintray {
        user = myBintrayUser
        key = myBintrayKey
        publications = ["mainJava", "sourcesJava", "mavenJavadoc"]
        pkg {
            repo = "maven"
            name = project.name
            licenses = ["Apache-2.0"]
            vcsUrl = "https://github.com/marklogic-community/" + project.name + ".git"
            version {
                name = project.version
                released = new Date()
            }
        }
    }
}

task copyTableauSourceFiles(type: Copy, group: taskGroup) {
    description = "Copy Tableau-dependent source files into the main source directory so they can be compiled and packaged into a jar for publishing. " +
            "You'll first need to add the Tableau jars to the 'dependencies' block as described in src/tableau/README.md ."
    from "src/tableau/main/java"
    into "src/main/java"
}

task createTableauPublication(group: taskGroup) {
    description = "Publish a version of ${project.name} that includes the Tableau-dependent source files under src/tableau/main/java"
    dependsOn = ["copyTableauSourceFiles", "publishToMavenLocal"]
    doLast {
        println "Version ${version} of ${project.name} has been published to your local Maven repository, which defaults to ~/.m2/repository."
        println "You can depend on this artifact in another Gradle project by adding mavenLocal() to your list of repositories."
    }
}
publishToMavenLocal.mustRunAfter copyTableauSourceFiles

// See https://github.com/snyk/gradle-plugin for more information
snyk {
    //arguments = '--all-sub-projects'
    severity = 'low'
    api = snykToken
    autoDownload = true
    autoUpdate = true
}
